Процедура ЗарегистрироватьИзмененияДанных(УзелОбмена)
	СоставПланаОбмена = УзелОбмена.Метаданные().Состав;
	Для Каждого ЭлементСоставаПланаОбмена из СоставПланаОбмена Цикл
		ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, ЭлементСоставаПланаОбмена.Метаданные);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВыполнитьОбмен(КодУстройства, ДанныеУстройства)
	УстановитьПривилегированныйРежим(Истина);
	
	//1. Инициализация (если нужно)
	УзелОбмена = ПланыОбмена.Мобильные.ЭтотУзел().ПолучитьОбъект();
	Если НЕ ЗначениеЗаполнено(УзелОбмена.Код) Тогда
		УзелОбмена.Код="001";
		УзелОбмена.Наименование="Центральный";
		УзелОбмена.Записать();
	КонецЕсли;
	
	УзелОбмена=ПланыОбмена.Мобильные.НайтиПоКоду(КодУстройства);
	Если УзелОбмена.Пустая() Тогда 
		НовыйУзел=ПланыОбмена.Мобильные.СоздатьУзел();
		НовыйУзел.Код=КодУстройства;
		НовыйУзел.Наименование=КодУстройства;
		НовыйУзел.Записать();
		ЗарегистрироватьИзмененияДанных(НовыйУзел.Ссылка);
		УзелОбмена=НовыйУзел.Ссылка;
	КонецЕсли;
	
	//2. Получение данных из мобильного устройства
	УзелОбмена = ПланыОбмена.Мобильные.НайтиПоКоду(КодУстройства);
	Сообщить(КодУстройства);
	ЧтениеXML=Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ДанныеУстройства.Получить());
	ЧтениеСообщения= ПланыОбмена.СоздатьЧтениеСообщения();
	ЧтениеСообщения.НачатьЧтение(ЧтениеXML);
	ПланыОбмена.УдалитьРегистрациюИзменений(ЧтениеСообщения.Отправитель,ЧтениеСообщения.НомерПринятого);
	НачатьТранзакцию();
	Пока ВозможностьЧтенияXML(ЧтениеXML) Цикл
		Данные = ПрочитатьXML(ЧтениеXML);
		Если НЕ Данные=Неопределено Тогда
			Данные.ОбменДанными.Отправитель=ЧтениеСообщения.Отправитель;
			Данные.ОбменДанными.Загрузка = Истина;
			Данные.Записать();
		КонецЕсли;
	КонецЦикла;
	ЗафиксироватьТранзакцию();
	ЧтениеСообщения.ЗакончитьЧтение();
	ЧтениеXML.Закрыть();
	
	//3. Выгрузка данных в мобильное устройство
	УзелОбмена = ПланыОбмена.Мобильные.НайтиПоКоду(КодУстройства);
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку("UTF-8");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
	ЗаписьСообщения.НачатьЗапись(ЗаписьXML,УзелОбмена);
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xsi","http://www/w3.org/2001/XMLSchema-instance");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("v8", "http://v8.1c.ru/data");
	ВыборкаИзменений = ПланыОбмена.ВыбратьИзменения(УзелОбмена, ЗаписьСообщения.НомерСообщения);
	Пока ВыборкаИзменений.Следующий() Цикл
		Данные = ВыборкаИзменений.Получить();
		ЗаписатьXML(ЗаписьXML, Данные);
	КонецЦикла;
	ЗаписьСообщения.ЗакончитьЗапись();
	Возврат Новый ХранилищеЗначения(ЗаписьXML.Закрыть(), Новый СжатиеДанных(9));
	
КонецФункции //ВыполнитьОбмен
